(define (domain Sokoban)
 (:requirements :strips :typing :negative-preconditions :disjunctive-preconditions)
 (:types player box place)
 (:predicates
	(playerIsAt ?p - place)
	(boxIsAt ?p - place)
	(isEmpty ?p - place)
	(isRight ?p1 - place ?p2 - place)
	(isLeft ?p1 - place ?p2 - place)
	(isUp ?p1 - place ?p2 - place)
	(isDown ?p1 - place ?p2 - place)
 )
 (:action pMove
 	:parameters (?pFrom - place ?pTo - place)
	:precondition (and (playerIsAt ?pFrom)
			   		(not (isEmpty ?pFrom))
			   		    (or (isRight ?pFrom ?pTo)
			   		    (isLeft ?pFrom ?pTo)
			   		    (isUp ?pFrom ?pTo)
			   		    (isDown ?pFrom ?pTo))
			   		(isEmpty ?pTo))
	:effect (and (playerIsAt ?pTo)
	            (not (playerIsAt ?pFrom))
		        (not (isEmpty ?pTo))
		        (isEmpty ?pFrom)))
 (:action push
	:parameters (?pFrom - place ?bFrom - place ?bTo - place)
	:precondition (and (playerIsAt ?pFrom)
			   (boxIsAt ?bFrom)
			   (or (and (isRight ?pFrom ?bFrom) (isRight ?bFrom ?bTo))
			       (and (isLeft ?pFrom ?bFrom) (isLeft ?bFrom ?bTo))
			       (and (isUp ?pFrom ?bFrom) (isUp ?bFrom ?bTo))
			       (and (isDown ?pFrom ?bFrom) (isDown ?bFrom ?bTo)))
			   (isEmpty ?bTo))
	:effect (and (playerIsAt ?bFrom)
	         (not (playerIsAt ?pFrom))
		     (boxIsAt ?bTo)
		     (not (boxIsAt ?bFrom))
		     (isEmpty ?pFrom)
		     (not (isEmpty ?bTo))))
)
